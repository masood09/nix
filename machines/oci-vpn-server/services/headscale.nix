{
  config,
  lib,
  pkgs,
  ...
}: let
  domain = "mantannest.com";
  headscaleDomain = "headscale.${domain}";
  # authDomain = "auth.${domain}";
in {
  options.services.headscale = {
    adminUser = lib.mkOption {
      type = lib.types.str;
      default = "admin";
      description = "Username for the headscale admin user";
    };
  };

  config = let
    inherit (config.services.headscale) adminUser;

    aclConfig = {
      # Groups definition
      groups = {
        "group:admins" = [adminUser];
      };

      acls = [
        # Allow all connections within the tailnet
        {
          action = "accept";
          src = ["*"];
          dst = ["*:*"];
        }
        # Allow admin to connect to their own services
        {
          action = "accept";
          src = [adminUser];
          dst = ["${adminUser}:*"];
        }
      ];

      # Auto-approvers section for routes
      autoApprovers = {
        routes = {
          "0.0.0.0/0" = [adminUser];
          "10.0.0.0/16" = [adminUser];
          "172.16.0.0/24" = [adminUser];
        };

        exitNode = [adminUser];
      };
    };

    # Convert to HuJSON format with comments
    aclHuJson = ''
      // Headscale ACL Policy - Generated by NixOS
      // Admin user: ${adminUser}

      ${builtins.toJSON aclConfig}
    '';

    aclFile = pkgs.writeText "acl-policy.hujson" aclHuJson;
  in {
    sops.secrets = {
      # "headscale-authentik-client-secret" = {
      #   owner = "headscale";
      #   sopsFile = ./../../../secrets/oci-vpn-server.yaml;
      # };

      "headscale-authelia-client-secret" = {
        owner = "headscale";
        sopsFile = ./../../../secrets/oci-vpn-server.yaml;
      };

      "restic-env" = {
        sopsFile = ./../../../secrets/oci-vpn-server.yaml;
      };
      "restic-repo" = {
        sopsFile = ./../../../secrets/oci-vpn-server.yaml;
      };
      "restic-password" = {
        sopsFile = ./../../../secrets/oci-vpn-server.yaml;
      };
    };

    services = {
      headscale = {
        enable = true;
        adminUser = "admin";
        port = 3009;
        settings = {
          log.level = "debug";

          dns = {
            base_domain = "dns.headscale.mantannest.com";

            extra_records = [
              {
                name = "grafana.mantannest.com";
                type = "A";
                value = "100.64.0.8";
              }
              {
                name = "ldap.internal.mantannest.com";
                type = "A";
                value = "100.64.0.9";
              }
              {
                name = "passwords.mantannest.com";
                type = "A";
                value = "100.64.0.4";
              }
              {
                name = "loki.monitoring.server.mantannest.com";
                type = "A";
                value = "100.64.0.8";
              }
            ];
          };

          logtail.enabled = false;

          oidc = {
            only_start_if_oidc_is_available = true;
            issuer = "https://auth2.mantannest.com";
            client_id = "authelia-headscale";
            client_secret_path = config.sops.secrets."headscale-authelia-client-secret".path;

            scope = [
              "openid"
              "profile"
              "email"
              "groups"
            ];

            pkce = {
              enabled = true;
              method = "S256";
            };

            # strip_email_domain = true;
          };

          # oidc = {
          #   only_start_if_oidc_is_available = true;
          #   issuer = "https://${authDomain}/application/o/headscale/";
          #   client_id = "Pjad107mj4JsZRnmbTMzbGiNqIolCMFn2jF3dBeA";
          #   client_secret_path = config.sops.secrets."headscale-authentik-client-secret".path;

          #   scope = [
          #     "openid"
          #     "profile"
          #     "email"
          #     "custom"
          #   ];

          #   pkce = {
          #     enabled = true;
          #     method = "S256";
          #   };

          #   strip_email_domain = true;
          # };

          policy.path = aclFile;
          server_url = "https://${headscaleDomain}";
        };
      };

      restic.backups.localBackup = {
        initialize = true;
        environmentFile = config.sops.secrets."restic-env".path;
        repositoryFile = config.sops.secrets."restic-repo".path;
        passwordFile = config.sops.secrets."restic-password".path;

        paths = [
          "/var/lib/headscale"
        ];

        pruneOpts = [
          "--keep-daily 24"
          "--keep-weekly 7"
          "--keep-monthly 30"
          "--keep-yearly 12"
        ];

        timerConfig = {
          OnCalendar = "*-*-* *:30:00";
          Persistent = true;
        };
      };
    };

    environment.persistence."/nix/persist" = {
      directories = [
        "/var/lib/headscale/"
      ];
    };
  };
}
