{
  config,
  lib,
  pkgs,
  ...
}: let
  domain = "mantannest.com";
  headscaleDomain = "headscale.${domain}";
  authDomain = "auth.${domain}";

  homelabCfg = config.homelab;
in {
  options.services.headscale = {
    adminUser = lib.mkOption {
      type = lib.types.str;
      default = "admin@ahmedmasood.com";
      description = "Username for the headscale admin user";
    };
  };

  config = let
    inherit (config.services.headscale) adminUser;

    aclConfig = {
      # Groups definition
      groups = {
        "group:admins" = [adminUser];
      };

      acls = [
        # Allow all connections within the tailnet
        {
          action = "accept";
          src = ["*"];
          dst = ["*:*"];
        }
        # Allow admin to connect to their own services
        {
          action = "accept";
          src = [adminUser];
          dst = ["${adminUser}:*"];
        }
      ];
    };

    # Convert to HuJSON format with comments
    aclHuJson = ''
      // Headscale ACL Policy - Generated by NixOS
      // Admin user: ${adminUser}

      ${builtins.toJSON aclConfig}
    '';

    aclFile = pkgs.writeText "acl-policy.hujson" aclHuJson;
  in {
    sops.secrets = {
      "headscale-authentik-client-secret" = {
        owner = "headscale";
        sopsFile = ./../../../secrets/oci-vpn-server.yaml;
      };
    };

    services = {
      headscale = {
        enable = true;

        settings = {
          dns = {
            override_local_dns = false;

            base_domain = "dns.headscale.mantannest.com";

            extra_records = [
              {
                name = "grafana.mantannest.com";
                type = "A";
                value = "100.64.0.7";
              }
              {
                name = "keep.mantannest.com";
                type = "A";
                value = "100.64.0.13";
              }
              {
                name = "homeassistant.mantannest.com";
                type = "A";
                value = "100.64.0.13";
              }
              {
                name = "photos.mantannest.com";
                type = "A";
                value = "100.64.0.16";
              }
              {
                name = "passwords.mantannest.com";
                type = "A";
                value = "100.64.0.3";
              }
              {
                name = "loki.monitoring.server.mantannest.com";
                type = "A";
                value = "100.64.0.7";
              }
              {
                name = "prometheus.monitoring.server.mantannest.com";
                type = "A";
                value = "100.64.0.7";
              }
            ];
          };

          logtail.enabled = false;

          oidc = {
            only_start_if_oidc_is_available = true;
            issuer = "https://${authDomain}/application/o/headscale/";
            client_id = "Pjad107mj4JsZRnmbTMzbGiNqIolCMFn2jF3dBeA";
            client_secret_path = config.sops.secrets."headscale-authentik-client-secret".path;

            scope = [
              "openid"
              "profile"
              "email"
              "custom"
            ];

            pkce = {
              enabled = true;
              method = "S256";
            };
          };

          policy.path = aclFile;
          server_url = "https://${headscaleDomain}";
        };
      };
    };

    environment.persistence."/nix/persist" = lib.mkIf (!homelabCfg.isRootZFS) {
      directories = [
        "/var/lib/headscale/"
      ];
    };
  };
}
